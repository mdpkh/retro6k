; Retro6K System Firmware
; special system addresses
VIDEOSTATE = $02FC
FVMCDEST = $03F8
FVMCSRC = $03F9
; code starts here
  .org $F000
  NOP
  NOP
  NOP
  NOP
entry: ; system power-on or reset
  ; misc initialization
  SEI
  CLD
  LDA #$00
  STA $03F8
  JSR WAITVSCANX
  ; display cartridge identification in bottom of screen
  LDA #$20
  STA $0A0A
  LDA $2000
  STA $0A0B
  LDA $2001
  STA $0A0C
  LDA $2002
  STA $0A0D
  LDA $2003
  STA $0A0E
  LDA $2004
  STA $0A0F
  LDA $2005
  STA $0A10
  LDA $2006
  STA $0A11
  LDA $2007
  STA $0A12
  LDA $2008
  STA $0A13
  LDA $2009
  STA $0A14
  LDA #$20
  STA $0A15
  LDX #$00
cartidcolloop:
  LDA #$F0
  STA $0B2A,X
  STA $0C4A,X
  STA $0D6A,X
  LDA #$50
  STA $0E8A,X
  STA $0FAA,X
  LDA #$53
  STA $0B4A,X
  STA $0C6A,X
  STA $0D8A,X
  LDA #$A0
  STA $0EAA,X
  STA $0FCA,X
  LDA #$02
  STA $09EA,X
  STA $0A2A,X
  INX
  TXA
  CMP #$0C
  BNE cartidcolloop
  LDA #$00
  STA $0FE0
  STA $0FE1
  STA $0FE2
  STA $0FE3
  STA $0FE4
  STA $0FE5
  STA $0FE6
  STA $0FE8
  STA $0FE9
  STA $0FEA
  STA $0FEB
  STA $0FEC
  STA $0FED
  STA $0FEE
  STA $0FEF
  STA $0FF0
  STA $0FF1
  STA $0FF2
  STA $0FF3
  STA $0FF4
  STA $0FF5
  STA $0FF6
  STA $0FF7
  STA $0FF9
  STA $0FFA
  STA $0FFB
  STA $0FFC
  STA $0FFD
  STA $0FFE
  LDA #$0F
  STA $0FE7
  LDA #$F0
  STA $0FF8
  LDA #$FF
  STA $0FFF
  ; detect presence of valid cartridge by checking for magic bytes 47 A9 0C 1E
  LDA $200A
  CMP #$47
  BNE nocart
  LDA $200B
  CMP #$A9
  BNE nocart
  LDA $200C
  CMP #$0C
  BNE nocart
  LDA $200D
  CMP #$1E
  BNE nocart
  ; if we got this far, jump to cartridge start vector
  JMP ($200E)
nocart: ; no cartridge inserted
  ; copy system font (low half)
  LDY #$E0 ; system font source page start
  LDA #$10 ; video RAM font page start
copyfontpageloop1:
  JSR WAITVSCANX ; wait for vertical retrace (preserve A & Y registers please)
  ; enable fast video memory copy
  CLC
  STA FVMCDEST
  STY FVMCSRC
  LDX #$00
copyfontbyteloop1:
  STA $00,X ; actual address and value written is overridden by fast video memory copy circuit
  INX
  STA $00,X
  INX
  STA $00,X
  INX
  STA $00,X
  INX
  BNE copyfontbyteloop1
  ; disable fast video memory copy
  ; X should be zero
  STX FVMCDEST
  INY ; increment source page
  CLC
  ADC #$01 ; increment destination page
  CMP #$18 ; compare against stop page number
  BNE copyfontpageloop1
  ; copy start screen graphics and custom chars
  LDY #$FA ; system font source page start
  LDA #$18 ; video RAM font page start
copyfontpageloop2:
  JSR WAITVSCANX ; wait for vertical retrace (preserve A & Y registers please)
  ; enable fast video memory copy
  CLC
  STA FVMCDEST
  STY FVMCSRC
  LDX #$00
copyfontbyteloop2:
  STA $00,X ; actual address and value written is overridden by fast video memory copy circuit
  INX
  STA $00,X
  INX
  STA $00,X
  INX
  STA $00,X
  INX
  BNE copyfontbyteloop2
  ; disable fast video memory copy
  ; X should be zero
  STX FVMCDEST
  INY ; increment source page
  CLC
  ADC #$01 ; increment destination page
  CMP #$1C ; compare against stop page number
  BNE copyfontpageloop2
  JSR WAITVSCANA ; wait for vertical retrace
  ; initialize screen display
  LDX #$00
initscreenloop:
  LDA datasystext0,X ; screen line 0: system startup text
  STA $0800,X
  LDA datasystext1,X ; screen line 1: system startup text
  STA $0820,X
  LDA datasystext2,X ; screen line 2: system startup text
  STA $0840,X
  LDA datasystext3,X ; screen line 3: system startup text
  STA $0860,X
  LDA datasystext1,X ; screen line 4: system startup text
  STA $0880,X
  LDA datasystext5,X ; screen line 5: system startup text
  STA $08A0,X
  LDA datasystext6,X ; screen line 6: system startup text
  STA $08C0,X
  LDA datasystext7,X ; screen line 7: system startup text
  STA $08E0,X
  LDA datasystext8,X ; screen line 8: system startup text
  STA $0900,X
  LDA #$20 ; screen lines 9-12: blank
  STA $0920,X
  STA $0940,X
  STA $0960,X
  STA $0980,X
  LDA datasystext4,X ; screen line 13: logo placeholder
  STA $09A0,X
  LDA #$20 ; screen lines 14-17: blank
  STA $09C0,X
  STA $09E0,X
  STA $0A00,X
  STA $0A20,X
  LDA #$FC ; screen lines 0-17 attribute bit 0: 0/1 0/1 1/1 1/1
  STA $0A40,X
  STA $0A60,X
  STA $0A80,X
  STA $0AA0,X
  STA $0AC0,X
  STA $0AE0,X
  STA $0B00,X
  STA $0B20,X
  STA $0B40,X
  LDA #$CC ; screen lines 2-17 attribute bit 1: 0/0 0/0 1/1 1/1
  STA $0B60,X
  STA $0B80,X
  STA $0BA0,X
  STA $0BC0,X
  STA $0BE0,X
  STA $0C00,X
  STA $0C20,X
  STA $0C40,X
  STA $0C60,X
  ; screen lines 0-17 attribute bit 2: 0/0 0/0 1/1 1/1
  STA $0C80,X
  STA $0CA0,X
  STA $0CC0,X
  STA $0CE0,X
  STA $0D00,X
  STA $0D20,X
  STA $0D40,X
  STA $0D60,X
  ; screen lines 0-5 attribute bit 3: 0/0 0/0 1/1 1/1
  STA $0D80,X
  STA $0DA0,X
  STA $0DC0,X
  STA $0DE0,X
  LDA #$C0 ; screen lines 6&7 attribute bit 3: 0/0 0/0 0/1 0/1
  STA $0E00,X
  LDA #$00 ; screen lines 8-17 attribute bit 3: 0/0 0/0 0/0 0/0
  STA $0E20,X
  STA $0E40,X
  STA $0E60,X
  STA $0E80,X
  STA $0EA0,X
  LDA #$CC ; screen lines 0-5 attribute bit 4: 0/0 0/0 1/1 1/1
  STA $0EC0,X
  STA $0EE0,X
  STA $0F00,X
  LDA #$0C ; screen lines 6&7 attribute bit 4: 0/0 0/0 1/0 1/0
  STA $0F20,X
  LDA #$00 ; screen lines 8-17 attribute bit 4: 0/0 0/0 0/0 0/0
  STA $0F40,X
  STA $0F60,X
  STA $0F80,X
  STA $0FA0,X
  STA $0FC0,X
  LDA datasyscolors,X ; palette colors
  STA $0FE0,X
  INX
  TXA
  AND #$03
  CMP #$00
  BNE initscreenloop
  JSR WAITVSCANA
  TXA
  CMP #$20
  BNE initscreenloop
  ; copy a few extra characters from Bank E font to video memory
  LDA #$10 ; destination page: screen font row 0
  STA FVMCDEST
  LDA #$EB ; source page: Bank E font row B
  STA FVMCSRC
  STA $C0 ; copy column C: copyright sign to position $0C
  STA $C1
  STA $C2
  STA $C3
  STA $C4
  STA $C5
  STA $C6
  STA $C7
  STA $C8
  STA $C9
  STA $CA
  STA $CB
  STA $CC
  STA $CD
  STA $CE
  STA $CF
  LDA #$EA ; source page: Bank E font row A
  STA FVMCSRC
  STA $E0 ; copy column E: diamond outline to position $0E
  STA $E1
  STA $E2
  STA $E3
  STA $E4
  STA $E5
  STA $E6
  STA $E7
  STA $E8
  STA $E9
  STA $EA
  STA $EB
  STA $EC
  STA $ED
  STA $EE
  STA $EF
  LDA #$EE ; source page: Bank E font row E
  STA FVMCSRC
  LDA #$16 ; destination page: screen font row 6
  STA FVMCDEST
  STA $00 ; copy column 0: en dash to position $60
  STA $01
  STA $02
  STA $03
  STA $04
  STA $05
  STA $06
  STA $07
  STA $08
  STA $09
  STA $0A
  STA $0B
  STA $0C
  STA $0D
  STA $0E
  STA $0F
  LDA #$00 ; disable FVMC
  STA FVMCDEST
  STA FVMCSRC
  JSR WAITVSCANA
  
halt:
  JMP halt

  .org $F3CA
WAITVSCANA:
; Subroutine: Wait for start of vertical blanking interval.
; Side effects: Accumulator is clobbered.
waitvscanaloop:
  LDA VIDEOSTATE
  AND #$30
  CMP #$20
  BNE waitvscanaloop
  RTS
WAITVSCANX:
; Subroutine: Wait for start of vertical blanking interval.
; Side effects: X register is clobbered.
  TAX
waitvscanxloop:
  LDA VIDEOSTATE
  AND #$30
  CMP #$20
  BNE waitvscanxloop
  TXA
  RTS
WAITVSCANY:
; Subroutine: Wait for start of vertical blanking interval.
; Side effects: Y register is clobbered.
  TAY
waitvscanyloop:
  LDA VIDEOSTATE
  AND #$30
  CMP #$20
  BNE waitvscanyloop
  TYA
  RTS

  .org $F3EC
BYTETOHEX:
; Subroutine: Convert value in Accumulator to uppercase hexadecimal.
; Side effects: result stored in zp$86,$87, neg digit count stored in X
  TAX
  AND #$0F ; extract low nybble
  ADC #$30 ; convert to numeric character
  CMP #$3A ; N := (A in 30-39); C := (A in 3A-3F)
  BMI bytetohexb1
  ADC #$06 ; if lo nybble > 9, convert to letter (C is set; effectively add 7)
bytetohexb1:
  STA $87 ; write low nybble character
  TXA
  AND #$F0 ; extract high nybble
  BEQ bytetohexb3 ; if high nybble is zero, jump
  LSR
  LSR
  LSR
  LSR ; shift nybble into place
  ADC #$30 ; convert to numeric character
  CMP #$3A ; N := (A in 30-39); C := (A in 3A-3F)
  BMI bytetohexb2
  ADC #$06 ; if hi nybble > 9, convert to letter (C is set; effectively add 7)
bytetohexb2:
  STA $86 ; write high nybble character
  TXA ; restore Accumulator
  LDX #$FE ; set X to -2 (2 digits)
  RTS
bytetohexb3:
  LDA #$30
  STA $86 ; write high zero
  TXA ; restore Accumulator
  LDX #$FF ; set X to -1 (1 digit)
  RTS

  .org $F418
; Subroutine: Convert value in Accumulator to decimal. Several entry points.
; Side effects: result stored in zp$8C,$8D,$8E,$8F, neg digit count stored in X, accumulator clobbered
BYTE2DECN:
  ; treat value as twos-complement negative number
  EOR #$FF ; find negative number
  CLC
  ADC #$01
  BCS bytetodecb10 ; negative zero? write literal "-256" instead
  STA $8F ; store negative number in zeropage
  LDA #$60 ; load minus sign for sign column
  STA $8C ; output sign column
  SED
  LDA #$00 ; initialize units and tens digits in accumulator
  STA $8D ; initialize hundreds digit in output position
  JMP bytetodecj9
bytetodecb10:
  STA $8C ; output - (sign column)
  LDA #$32
  STA $8D ; output 2 (hundreds column)
  LDA #$35
  STA $8E ; output 5 (hundreds column)
  LDA #$36
  STA $8F ; output 6 (hundreds column)
  LDA #$00 ; restore Accumulator
  LDX #$FD ; set X to -3 (3 digits)
  RTS
BYTE2DECUS:
  ; Treat value as result of unsigned subtraction. Branch on carry flag...
  BCC BYTE2DECN
  JMP BYTE2DECP
BYTE2DECS:
  ; Treat value as result of signed subtraction. Branch on overflow and negative flags...
  BVS bytetodecb8
  BMI BYTE2DECN
  JMP BYTE2DECP
bytetodecb8:
  BPL BYTE2DECN
  JMP BYTE2DECP
BYTE2DECUA:
  ; Treat value as result of unsigned addition (or 9-bit unsigned value). Branch on carry flag...
  BCC BYTE2DECP
BYTE2DECBP:
  ; Treat value as unsigned, add 256 (range 256-511)
  STA $8F ; place input in zeropage (temporarily using units digit output byte)
  LDA #$20 ; load space for sign column
  STA $8C ; output sign column
  LDA #$02
  STA $8D ; initialize hundreds digit in output position
  LDA #$56 ; initialize units and tens digits in accumulator
  JMP bytetodecj9
BYTE2DECP:
  ; Treat value as unsigned in range 0-255
  STA $8F ; place input in zeropage (temporarily using units digit output byte)
  LDA #$20 ; load space for sign column
  STA $8C ; output sign column
  LDA #$00 ; initialize units and tens digits in accumulator
  STA $8D ; initialize hundreds digit in output position
bytetodecj9:
  SED ; begin adding data bits
  LSR $8F ; shift input
  BCC bytetodecb0
  CLC
  ADC #$01 ; add bit value
bytetodecb0:
  LSR $8F ; shift input
  BCC bytetodecb1
  CLC
  ADC #$02 ; add bit value
bytetodecb1:
  LSR $8F ; shift input
  BCC bytetodecb2
  CLC
  ADC #$04 ; add bit value
bytetodecb2:
  LSR $8F ; shift input
  BCC bytetodecb3
  CLC
  ADC #$08 ; add bit value
bytetodecb3:
  LSR $8F ; shift input
  BCC bytetodecb4
  CLC
  ADC #$16 ; add bit value
bytetodecb4:
  LSR $8F ; shift input
  BCC bytetodecb5
  CLC
  ADC #$32 ; add bit value
  BCC bytetodecb5
  CLC
  INC $8D ; increment hundreds digit if this bit caused a carry (possible in Big Positive number format)
bytetodecb5:
  LSR $8F ; shift input
  BCC bytetodecb6
  CLC
  ADC #$64 ; add bit value
  BCC bytetodecb6
  CLC
  INC $8D ; increment hundreds digit if this bit caused a carry
bytetodecb6:
  LSR $8F ; shift input
  BCC bytetodecb7
  CLC
  INC $8D ; increment hundreds digit
  ADC #$28 ; add 28
  BCC bytetodecb7
  CLC
  INC $8D ; increment hundreds digit if the 28 caused another carry
bytetodecb7:
  CLD
  LDX $8D ; is hundreds digit zero?
  BNE bytetodecb13
  LDX #$30
  STX $8D ; output numeral zero in hundreds column
  TAX ; move units and tens digits out of the way
  AND #$F0 ; retrieve tens digit; is it zero?
  BNE bytetodecb12
  TXA
  LDX #$30
  STX $8E ; output numeral zero in tens column
  AND #$0F ; retrieve units digit
  ADC #$30 ; convert to numeral
  STA $8F ; output units digit
  LDX #$FF ; set X to -1 (1 digit)
  RTS
bytetodecb12:
  LSR
  LSR
  LSR
  LSR ; shift digit into place
  ADC #$30 ; convert to numeral
  STA $8E ; output tens digit
  TXA
  AND #$0F ; retrieve units digit
  ADC #$30 ; convert to numeral
  STA $8F ; output units digit
  LDX #$FE ; set X to -2 (2 digits)
  RTS
bytetodecb13:
  TAX ; move units and tens digits out of the way
  LDA $8D ; retrieve hundreds digit
  ADC #$30 ; convert to numeral
  STA $8D ; output hundreds digit
  TXA
  AND #$F0 ; retrieve tens digit
  LSR
  LSR
  LSR
  LSR ; shift digit into place
  ADC #$30 ; convert to numeral
  STA $8E ; output tens digit
  TXA
  AND #$0F ; retrieve units digit
  ADC #$30 ; convert to numeral
  STA $8F ; output units digit
  LDX #$FD ; set X to -3 (3 digits)
  RTS


  .org $F500
datagfx:
  .incbin firmwaregfx.bin,$0,$0900
datasystext0:
  .text "\056 \276\277 \200\201 \056  \016  \016  \016  \016  \056 \276\277 \200\201 \056"
datasystext1:
  .text "                                "
datasystext2:
  .text "\217   Retro 6k Fantasy Computer  \217"
datasystext3:
  .text "\260     Entertainment System\021    \260"
datasystext4:
  .text "         \031 Logo  here \020         "
datasystext5:
  .text "\056 \276\277 \200\201 \056  \016  \016  \016  \016  \056 \276\277 \200\201 \056"
datasystext6:
  .text "   Please insert a cartridge>   "
datasystext7:
  .text "     F\267\270\271ware version 1985d     "
datasystext8:
  .text "\0142019\14020 Maggie David \206\207\210 Haynes"
datasyscolors:
  .byte $00,$81,$84,$0D,$02,$83,$86,$0F
  .byte $D0,$59,$5C,$DD,$D2,$5B,$5E,$DF
  .byte $20,$A1,$A4,$2D,$22,$A3,$A6,$2F
  .byte $F0,$79,$7C,$FD,$F2,$7B,$7E,$FF

  .org $FFF9
nmicode: ; empty, because NMI not presently used
  RTI
  
; 6502 program counter initialization vectors
  .org $FFFA
  .word nmicode ; NMI vector, in firmware
  .word entry ; reset vector, in firmware
  .word $2010 ; interrupt request vector, in cartridge